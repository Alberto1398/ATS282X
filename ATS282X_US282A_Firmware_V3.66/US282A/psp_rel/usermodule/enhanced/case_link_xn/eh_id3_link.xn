INPUT(link_base.xn)

AP_INDEX = FRONT_AP;

BANK_FRONT_ENHANCED_1_ADDR_BASE = (AP_BANK_FRONT_ENHANCED_1 << 24) + SRAM_AP_BANK_FRONT_ENHANCED_1_ADDR;
BANK_UI_1_ADDR_BASE = (AP_BANK_FRONT_UI_1 << 24) + SRAM_AP_BANK_FRONT_UI_ADDR;

/*bank的实际空间大小,不能修改*/
BANK_FRONT_ENHANCED1_SIZE = SRAM_AP_BANK_FRONT_ENHANCED_1_SIZE;
BANK_UI_SIZE = SRAM_AP_BANK_FRONT_UI_SIZE;

/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)
EXTERN(vfs_op_entry)

SECTIONS
{
    /*常驻代码*/
    .text RCODE_TEXT_ADDR :
    {
        _resident_text = ABSOLUTE(.); /*只是标号*/
        /*4.无常驻代码*/
        _resident_text_end = ABSOLUTE(.);
    }

    . = BANK_FRONT_ENHANCED_1_ADDR_BASE;
    OFFSET = . & 0x3ffff;
    BANK_ENHANCED_ID3_1_0 :
    {
        eh_id3_entry.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_ENHANCED_ID3_1_1 :
    {
        eh_id3_function.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_ENHANCED_LRC_1_0 :
    {
        eh_lyric_load.o(.text .rodata .xdata)
    }

    . = BANK_UI_1_ADDR_BASE + AP_BANK_SPACE * 54;
    /*从BANK_UI_1_54开始，要确保common_front.xn里面最大不超过53*/
    OFFSET = . & 0x3ffff;
    BANK_UI_1_54 :
    {
        eh_id3_mp3_v1.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_55 :
    {
        eh_id3_mp3_v2.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_56 :
    {
        eh_id3_wma.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_57 :
    {
        eh_id3_ape.o(.text .rodata .xdata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
    BANK_UI_1_58 :
    {
        eh_id3_flac.o(.text .rodata .xdata)
        eh_id3_utf8_deal.o(.text .rodata .xdata)
    }
}
