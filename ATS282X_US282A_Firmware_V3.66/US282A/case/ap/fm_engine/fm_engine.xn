INPUT(link_base.xn)

AP_INDEX = BACK_AP;

SRAM_TEXT_ADDR = SRAM_AP_BACK_RCODE_ADDR;
RCODE_TEXT_ADDR = RCODE_ADDR_BASE + SRAM_TEXT_ADDR;
RCODE_SIZE = SRAM_AP_BACK_RCODE_SIZE;

SRAM_DATA_ADDR = SRAM_FM_ENG_RDATA_ADDR;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_DATA_ADDR;
DATA_BSS_SIZE = SRAM_FM_ENG_RDATA_SIZE;

/*这里只能用0~39 共40个bank, 40~63被common用了*/
BANK_CONTROL_1_ADDR_BASE = (AP_BANK_BACK_CONTROL_1 << 24) + SRAM_AP_BANK_BACK_CONTROL_ADDR;
BANK_CONTROL_SIZE = SRAM_AP_BANK_BACK_CONTROL_SIZE;
/*系统允许的bank的最大空间大小，不能修改*/
AP_BANK_SPACE = BANK_SPACE;

OUTPUT_ARCH(mips)
ENTRY(__start)

SECTIONS
{
    /*常驻代码*/
    .text RCODE_TEXT_ADDR :
    {
        /*API OBJ在此链接*/
        audio_device_op_entry.o(.text)
        mmm_pp_cmd.o(.text)
        fm_op_entry.o(.text)
        key_op_entry.o(.text)
        libc_op_entry.o(.text)
        sys_op_entry.o(.text)
                
        fmengine_control.o(.text .rodata)
        fmengine_message_set_deal_rcode.o(.text .rodata)
        fmengine_message_get_deal_rcode.o(.text .rodata)
        fmengine_main.o(.rodata)
        
        app_timer_rcode_for_engine.o(.text)
        message_rcode_for_engine.o(.text)
    }

    /* 初始化了的全局数据段*/
    .data RDATA_DATA_ADDR :  AT(ADDR(.text) + SIZEOF(.text))
    {
        _resident_data = ABSOLUTE(.);
        /*5.填写rdata输入节，所以文件产生的.data都被链接到这里*/
        fmengine_main.o(.data)
        fmengine_play_deal.o(.data)
    }
    /*未初始化的全局数据段，系统不会进行清零*/
    .bss (RDATA_DATA_ADDR + SIZEOF(.data)) :
    {
        fmengine_main.o(.bss)
        fmengine_play_deal.o(.bss)
        applib_app_data.o(.bss)
        *(.sbss)
        *(.common)
        *(common)
    }

    /*6.链接bank control 0代码*/
    . = BANK_CONTROL_1_ADDR_BASE;
    OFFSET = . & 0x3ffff;
    BANK_CONTROL_1_0 :
    {
        ctor.o(.text .rodata)
        fmengine_control.o(.bank)
    }=0x0/*对定位符的赋值而产生的空隙由0x0填充*/

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
     BANK_CONTROL_1_1 :
    {
        fmengine_message_set_deal.o(.text .rodata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
     BANK_CONTROL_1_2 :
    {
        fmengine_message_get_deal.o(.text .rodata)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
     BANK_CONTROL_1_3 :
    {
        fmengine_main.o(.text)
    }

    . = ((. + AP_BANK_SPACE) & (~(AP_BANK_SPACE - 1))) + OFFSET;
     BANK_CONTROL_1_4 :
    {
        fmengine_play_deal.o(.text)
        fmengine_message_volume_deal.o(.text .rodata)
    }
}
